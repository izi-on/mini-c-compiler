# #  comment
# () grouping
# [] optional
# *  zero or more
# +  one or more
# |  alternative

program    ::= (include)* (structdecl | vardecl | fundecl | fundef)* EOF

include    ::= "#include" STRING_LITERAL

structdecl ::= structtype "{" (vardecl)+ "}" ";"    # structure declaration

vardecl    ::= type IDENT ("[" INT_LITERAL "]")* ";" # variable declaration, (e.g. int a;), or multi-dimensional array declaration, (e.g. int a[2][5];)

fundef    ::= type IDENT "(" params ")" block       # function definition

fundecl   ::= type IDENT "(" params ")" ";"         # function declaration

type       ::= ("int" | "char" | "void" | structtype) ("*")*
structtype ::= "struct" IDENT

params     ::= [ type IDENT ("[" INT_LITERAL "]")* ("," type IDENT ("[" INT_LITERAL "]")*)* ]

stmt       ::= block
             | "while" "(" exp ")" stmt              # while loop
             | "if" "(" exp ")" stmt ["else" stmt]   # if then else
             | "return" [exp] ";"                    # return
             | exp ";"                               # expression statement, e.g. a function call
             | "continue" ";"                        # continue
             | "break" ";"                           # break

block      ::= "{" (vardecl)* (stmt)* "}"

exp        ::= "(" exp ")"
             | exp "=" exp                           # assignment
             | (IDENT | INT_LITERAL)
             | ("-" | "+") exp
             | CHAR_LITERAL
             | STRING_LITERAL
             | exp (">" | "<" | ">=" | "<=" | "!=" | "==" | "+" | "-" | "/" | "*" | "%" | "||" | "&&") exp  # binary operators
             | arrayaccess | fieldaccess | valueat | addressof | funcall | sizeof | typecast

funcall      ::= IDENT "(" [ exp ("," exp)* ] ")" # function call
arrayaccess  ::= exp "[" exp "]"                  # array access
fieldaccess  ::= exp "." IDENT                    # structure field member access

valueat      ::= "*" exp                          # Value at operator (pointer indirection)
addressof    ::= "&" exp                          # Address-of operator

_________________________________________
UNAMBIGUOUS GRAMMAR

program    ::= (include)* (structdecl | vardecl_funcdef_or_funcdecl)* EOF

include    ::= "#include" STRING_LITERAL

structdecl ::= structtype "{" (vardecl)+ "}" ";"    # structure declaration
vardecl    ::= type IDENT ("[" INT_LITERAL "]")* ";" # variable declaration, (e.g. int a;), or multi-dimensional array declaration, (e.g. int a[2][5];)

vardecl_funcdef_or_funcdecl ::= type IDENT (is_vardecl | is_fundef_or_fundecl)
is_vardecl    ::= ("[" INT_LITERAL "]")* ";" # variable declaration, (e.g. int a;), or multi-dimensional array declaration, (e.g. int a[2][5];)
is_fundef_or_fundecl ::= "(" params ")" (block | ";")

structtype ::= "struct" IDENT

params     ::= [ type IDENT ("[" INT_LITERAL "]")* ("," type IDENT ("[" INT_LITERAL "]")*)* ]

stmt ::= ifStmnt | nonIfStmnt
nonIfStmt    ::= block
             | "while" "(" exp ")" stmt              # while loop
             | "return" [exp] ";"                    # return
             | exp ";"                               # expression statement, e.g. a function call
             | "continue" ";"                        # continue
             | "break" ";"                           # break
ifStmtStart ::= "if" "(" exp ")" withOrWithoutElse
withOrWithoutElse ::= stmt
            | withElse "else" stmt
withElse ::= if "(" exp ")" withElse "else" withElse
            | nonIfStmt

block      ::= "{" (vardecl)* (stmt)* "}"

exp ::= exp1 "=" (exp | exp1)
exp1 ::= exp2 ("||" exp2)*
exp2 ::= exp3 ("&&" exp3)*
exp3 ::= exp4 (("==" | "!=") exp4)*
exp4 ::= exp5 ((">" | "<" | ">=" | "<=") exp5)*
exp5 ::= exp6 (("+" | "-") exp6)*
exp6 ::= exp7 (("*" | "/" | "%") exp7)*
exp7 ::= ("&" | "*" | typecast | "-" | "+") (exp7 | exp8)
exp8 ::= ( IDENT ( funcall_params | matrix_brqt | struct_field_access )* ) | ( exp9 (matrix_brqt | struct_field_access)* )
exp9 ::= "(" exp ")" | INT_LITERAL | CHAR_LITERAL | STRING_LITERAL | sizeof

sizeof       ::= "sizeof" "(" type ")"            # size of type
typecast     ::= "(" type ")" exp                 # type casting
type       ::= ("int" | "char" | "void" | structtype) ("*")*

funccall_params ::= "(" [ exp ("," exp)* ] ")"
matrix_brqt ::= "[" exp "]"
struct_field_access ::= "." IDENT

